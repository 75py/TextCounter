/*
 * Copyright 2018 75py
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.nagopy.android.textcounter"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode publish_version_code
        versionName publish_version_name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "AD_APP_ID", "\"ca-app-pub-2998260208076899~2635354566\""
        resValue "string", "banner_ad_unit_id", "ca-app-pub-2998260208076899/4112087763"
        resValue "string", "reward_ad_unit_id", "ca-app-pub-2998260208076899/8716305490"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    // https://github.com/jaredsburrows/android-gradle-kotlin-app-template/blob/master/build.gradle
    testOptions {
        animationsDisabled = true
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }
}

// https://github.com/arturdm/jacoco-android-gradle-plugin/issues/37
jacocoAndroidUnitTestReport {
    excludes += [
            '**/R.class',
            '**/R$*.class',
            '**/DataBindingInfo.class',
            '**/android/databinding/*.*',
            '**/com/android/**/*.*',
            'android/**/*.*',
            '**/BR.class',
            'com/nagopy/android/textcounter/databinding/*.*',
    ]
    csv.enabled true
    html.enabled true
    xml.enabled true
}

project.afterEvaluate {
    android.applicationVariants.all { variant ->
        def realVariantName = variant.name
        def variantName = variant.name.capitalize()
        def task = project.tasks["jacocoTest${variantName}UnitTestReport"]
        def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/${realVariantName}", excludes: task.classDirectories.excludes)
        task.classDirectories += kotlinDebugTree
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.github.salomonbrys.kodein:kodein:4.1.0'
    implementation 'com.github.salomonbrys.kodein:kodein-conf:4.1.0'
    implementation('com.github.salomonbrys.kodein:kodein-android:4.1.0') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    implementation 'com.jakewharton.timber:timber:4.6.0'
    kapt 'com.android.databinding:compiler:3.0.1'

    implementation 'com.google.firebase:firebase-ads:11.8.0'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.11.0"
    testImplementation "org.robolectric:robolectric:3.5.1"

    //androidTestImplementation 'com.android.support.test:runner:1.0.1'
    //androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

apply plugin: 'com.google.gms.google-services'
